#pragma kernel Init
#pragma kernel BitonicSortParticles
#pragma kernel ResetCellOffsets
#pragma kernel CalculateCellOffsets
#pragma kernel CalculateDensity
#pragma kernel CalculateForce
#pragma kernel CalculateKinematicForce
#pragma kernel Update
#pragma kernel Emit

#include "./ParticleCommon.cginc"

RWStructuredBuffer<Particle> _Particles;
StructuredBuffer<KinematicParticle> _KinematicParticles;
RWStructuredBuffer<float3> _KinematicForcesAndBuoyances;
RWStructuredBuffer<uint> _CellOffsets;

AppendStructuredBuffer<uint> _Dead;
ConsumeStructuredBuffer<uint> _Pool;
AppendStructuredBuffer<uint> _Alive;

StructuredBuffer<float4> _Uploads;

Texture2D<float3> _TerrainDistanceField;
SamplerState sampler_TerrainDistanceField;
float4 _TerrainDistanceFieldScale; // x = 1 / width, y = 1 / height, z = width, w = height

static const float G = -9.81;
static const float Wpoly6 = 1.56668147106;
static const float GradientWspiky = -14.3239448783;
static const float LaplacianWviscosity = 14.3239448783;

int _SortBlock;
int _SortDim;
uint _SortCount;

float _RestDensity;
float _PressureConstant;
float _Viscosity;

uint _KinematicCount;
float _KinematicViscosity;
float _Multiplier;

float _MinH;
float _MaxH;
float _Demultiplier;
float _DT;

uint _EmitCount;
float _LifeTime;

[numthreads(256,1,1)]
void Init (uint3 id : SV_DispatchThreadID)
{
	const uint idx = id.x;
	_Particles[idx].alive = false;
	_Dead.Append(idx);
}

static const uint INVALID_OFFSET = 0xffffffff;
static const uint GRID_PARTITION_BUCKET_COUNT = 1024 * 1024;
inline uint GridHash(int2 cellIndex) {
	const uint p1 = 73856093;   // some large primes 
	const uint p2 = 19349663;
	int n = p1 * cellIndex.x ^ p2 * cellIndex.y;
	n %= GRID_PARTITION_BUCKET_COUNT;
	return n;
}

inline uint GridHash(Particle p) {
	if (!p.alive) return GRID_PARTITION_BUCKET_COUNT;
	const int2 cellIndex = floor(p.position);
	return GridHash(cellIndex);
}


[numthreads(256,1,1)]
void BitonicSortParticles (uint3 id : SV_DispatchThreadID) {
	const uint i = id.x;
	const uint j = i^_SortBlock;
	
	if (j < i || i >= _SortCount) 
		return;
	
	Particle p_i = _Particles[i];
	Particle p_j = _Particles[j];
	const uint value_i = GridHash(p_i);
	const uint value_j = GridHash(p_j);
	
	const int diff = (value_i - value_j) * ((i&_SortDim) == 0 ? 1 : -1);
	if (diff > 0) {
		_Particles[i] = p_j;
		_Particles[j] = p_i;
	}
}

[numthreads(256,1,1)]
void ResetCellOffsets (uint3 id : SV_DispatchThreadID) {
	_CellOffsets[id.x] = INVALID_OFFSET;
}

[numthreads(256,1,1)]
void CalculateCellOffsets (uint3 id : SV_DispatchThreadID) {
	const uint idx = id.x;
	Particle p = _Particles[idx];
	if (p.alive) {
		const uint gridHash = GridHash(p);
		InterlockedMin(_CellOffsets[gridHash], idx);
	}
}

[numthreads(256,1,1)]
void CalculateDensity (uint3 id : SV_DispatchThreadID)
{
	const uint idx = id.x;
	Particle p = _Particles[idx];
	if (p.alive) {
		float density = 0;
		uint count, stride;
		_Particles.GetDimensions(count, stride);
		for (int x = -1; x < 2; ++x) {
			for (int y = -1; y < 2; ++y) {
				const uint gridHash = GridHash(floor(p.position) + float2(x, y));
				uint i = _CellOffsets[gridHash];
				while (i != INVALID_OFFSET && i < count) {
					Particle o = _Particles[i];
					if (gridHash != GridHash(o)) {
						break;
					}
					const float2 r = o.position - p.position;
					const float r2 = dot(r, r);
					if (r2 < 1) {
						const float q = 1 - r2;
						density += q * q * q;
					}
					i++;
				}
			}
		}

		p.density = max(_RestDensity, Wpoly6 * density);
		_Particles[idx] = p;
	}
}

[numthreads(256,1,1)]
void CalculateForce (uint3 id : SV_DispatchThreadID)
{
	const uint idx = id.x;
	Particle p = _Particles[idx];
	if (p.alive) {
		float2 pForce = float2(0, 0);
		float2 vForce = float2(0, 0);

		float pressure_p = p.density / _RestDensity;
		pressure_p = _PressureConstant * (pressure_p * pressure_p - 1);

		uint count, stride;
		_Particles.GetDimensions(count, stride);
		for (int x = -1; x < 2; ++x) {
			for (int y = -1; y < 2; ++y) {
				const uint gridHash = GridHash(floor(p.position) + float2(x, y));
				uint i = _CellOffsets[gridHash];
				while (i != INVALID_OFFSET && i < count) {
					if (i != idx) {
						Particle o = _Particles[i];
						if (gridHash != GridHash(o)) {
							break;
						}
						const float2 _r = o.position - p.position;
						const float r = max(0.001f, length(_r));
						if (r < 1) {
							float pressure_n = o.density / _RestDensity;
							pressure_n = _PressureConstant * (pressure_n * pressure_n - 1);
							const float q = 1 - r;
							pForce += _r / r * ((pressure_p + pressure_n) / (2 * o.density) * q * q);

							const float2 _v = o.velocity - p.velocity;
							vForce += _v * (1 / o.density * q);
						}
					}
					i++;
				}
			}
		}

		pForce *= GradientWspiky;
		vForce *= _Viscosity * LaplacianWviscosity;
		p.force = (pForce + vForce) / p.density;
		_Particles[idx] = p;
	}
}

[numthreads(256,1,1)]
void CalculateKinematicForce (uint3 id : SV_DispatchThreadID)
{
	const uint idx = id.x;
	if (idx < _KinematicCount) {
		KinematicParticle p = _KinematicParticles[idx];
		const float2 position = p.position * _Multiplier;
		const float2 velocity = p.velocity * _Multiplier;
		float density = 0;
		float2 vForce = float2(0, 0);

		uint count, stride;
		_Particles.GetDimensions(count, stride);
		for (int x = -1; x < 2; ++x) {
			for (int y = -1; y < 2; ++y) {
				const uint gridHash = GridHash(floor(position) + float2(x, y));
				uint i = _CellOffsets[gridHash];
				while (i != INVALID_OFFSET && i < count) {
					Particle o = _Particles[i];
					if (gridHash != GridHash(o)) {
						break;
					}
					const float2 _r = o.position - position;
					const float r2 = dot(_r, _r);
					if (r2 < 1) {
						const float q = 1 - r2;
						density += q * q * q;

						const float r = sqrt(r2);
						const float2 _v = o.velocity - velocity;
						const float d = o.density;
						vForce += _v * ((1 - r) / (d * d));
					}
					i++;
				}
			}
		}

		density = step(_RestDensity, density);
		vForce *= _KinematicViscosity * LaplacianWviscosity;
		_KinematicForcesAndBuoyances[idx] = float3(vForce, density);
	}
}

[numthreads(256,1,1)]
void Update (uint3 id : SV_DispatchThreadID)
{
	uint idx = id.x;
	Particle p = _Particles[idx];
	if (p.alive) {
		float h = length(p.position);
		p.life.x += _DT;
		if (p.life.x < p.life.y && h > _MinH && h < _MaxH) {
			float2 uv = _Demultiplier * p.position * _TerrainDistanceFieldScale.xy + 0.5;
			float3 td = 2 * _TerrainDistanceField.SampleLevel(sampler_TerrainDistanceField, uv, 0) - 1;
			float2 tForce = float2(0, 0);
			if (td.x < _Demultiplier) {
				float r = max(0, td.x) / _Demultiplier;
				float q = 1 - r;
				float2 _v = -p.velocity;
				tForce = _v * (_Viscosity * LaplacianWviscosity / (p.density * p.density) * q);
			}

			float2 up = p.position / h;
			p.velocity += (p.force + tForce + (G / _Demultiplier) * up) * _DT;
			if (td.x < 0) {
				float2 n = td.yz;
				float vn = max(0, -dot(p.velocity, n));
				p.velocity += 1.1 * vn * n;
				//p.velocity *= 0.9;
			}
			p.position += p.velocity * _DT;
			_Alive.Append(idx);
		} else {
			p.alive = false;
			_Dead.Append(idx);
		}
		_Particles[idx] = p;
	}
}

[numthreads(256,1,1)]
void Emit (uint3 id : SV_DispatchThreadID)
{
	uint idx = id.x;
	if (idx < _EmitCount) {
		float4 upload = _Uploads[idx];
		idx = _Pool.Consume();
		Particle p = _Particles[idx];
		p.alive = true;
		p.position = upload.xy;
		p.velocity = upload.zw;
		p.life = float2(0, _LifeTime);
		_Particles[idx] = p;
	}
}