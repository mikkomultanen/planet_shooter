#pragma kernel Init
#pragma kernel Update
#pragma kernel Emit

#include "./ParticleCommon.cginc"

RWStructuredBuffer<Particle> _Particles;

AppendStructuredBuffer<uint> _Dead;
ConsumeStructuredBuffer<uint> _Pool;
AppendStructuredBuffer<uint> _Alive;

StructuredBuffer<float4> _Uploads;

Texture2D<float3> _TerrainDistanceField;
SamplerState sampler_TerrainDistanceField;
float4 _TerrainDistanceFieldScale; // x = 1 / width, y = 1 / height, z = width, w = height

static const float G = -9.81;

float _MinH;
float _MaxH;
float _DT;

uint _EmitCount;
float _LifeTime;

[numthreads(16,1,1)]
void Init (uint3 id : SV_DispatchThreadID)
{
	uint idx = id.x;
	_Particles[idx].alive = false;
	_Dead.Append(idx);
}

[numthreads(16,1,1)]
void Update (uint3 id : SV_DispatchThreadID)
{
	uint idx = id.x;
	Particle p = _Particles[idx];
	if (p.alive) {
		float h = length(p.position);
		p.life.x += _DT;
		if (p.life.x < p.life.y && h > _MinH && h < _MaxH) {
			float2 uv = p.position * _TerrainDistanceFieldScale.xy + 0.5;
			float3 td = _TerrainDistanceField.SampleLevel(sampler_TerrainDistanceField, uv, 0);
			float2 up = p.position / h;
			p.velocity += G * up * _DT;
			if (td.x < 0.5) {
				//p.velocity = reflect(p.velocity, td.yz);
				float2 n = 2 * td.yz - 1;
				float vn = max(0, -dot(p.velocity, n));
				p.velocity += 2 * vn * n;
				p.velocity *= 0.9;
				//p.velocity = float2(0, 0);
			}
			p.position += p.velocity * _DT;
			_Alive.Append(idx);
		} else {
			p.alive = false;
			_Dead.Append(idx);
		}
		_Particles[idx] = p;
	}
}

[numthreads(16,1,1)]
void Emit (uint3 id : SV_DispatchThreadID)
{
	uint idx = id.x;
	if (idx < _EmitCount) {
		float4 upload = _Uploads[idx];
		idx = _Pool.Consume();
		Particle p = _Particles[idx];
		p.alive = true;
		p.position = upload.xy;
		p.velocity = upload.zw;
		p.life = float2(0, _LifeTime);
		_Particles[idx] = p;
	}
}